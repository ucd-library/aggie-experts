prefix : <http://oapolicy.universityofcalifornia.edu/vocab#>
prefix cdl: <ark:/87287/d7_cdltest_/>
prefix experts: <http://experts.ucdavis.edu/>
prefix oap: <http://oapolicy.universityofcalifornia.edu/>
prefix person: <http://experts.ucdavis.edu/person/>
prefix ucdlib: <http://schema.library.ucdavis.edu/schema#>
prefix vivo: <http://vivoweb.org/ontology/core#>

construct {
  ?authorship a vivo:Authorship,ucdlib:Authorship;
              ucdlib:favorite ?favorite;
              ucdlib:is-visible true;
              vivo:relates ?expert,?publication;
    .
}
WHERE {
  graph oap: {
    ?RELATIONSHIP__ :type "publication-user-authorship";
                  :effective-privacy-level "Public";
                  :is-visible "true";
                  :last-modified-when ?modified;
                  :related ?pub;
                  :related ?user;
                  .
    ?pub :category "publication";
         .
    ?user :category "user";
          :username ?username;
          .

    OPTIONAL {
	    ?RELATIONSHIP__ :is-favourite "true".
    	bind(true as ?favorite)
    }
  }
  bind(uri(replace(str(?RELATIONSHIP__),str(oap:),concat(str(cdl:),"relationships/"))) as ?authorship)
  bind(uri(concat(str(cdl:),"publication/",replace(str(?pub),str(oap:),""))) as ?publication)
  bind(uri(concat(str(person:),md5(?username))) as ?expert)
}
