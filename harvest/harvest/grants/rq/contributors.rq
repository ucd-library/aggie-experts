PREFIX : <ark:/87287/d7gt0q/schema#>
PREFIX experts: <http://experts.ucdavis.edu/>
PREFIX grant: <ark:/87287/d7gt0q/grant/>
PREFIX kfs: <ark:/87287/d7gt0q/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX expert: <http://experts.ucdavis.edu/expert/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX vivo: <http://vivoweb.org/ontology/core#>

CONSTRUCT {
	?grant vivo:relates ?role.

  ?role a ?vivo_role, ?ucd_role;
	        vivo:relatedBy ?grant;
	       obo:RO_0000052 ?expert;
        rdfs:label ?role_label;
        .

  ?expert obo:RO_0000053 ?role;
          .
}
WHERE {
  VALUES (?grant_role ?ucd_role ?vivo_role) {
    ("PDIR" :GrantProgramDirectorRole vivo:LeaderRole)
    ("KP" :GrantKeyPersonnelRole vivo:ResearcherRole)
    ("OTHR" :GrantOtherRole vivo:ResearcherRole)
    ("PLDR" :GrantProjectLeaderRole vivo:LeaderRole)
    ("COPI" :GrantCoPrincipalInvestigatorRole vivo:CoPrincipalInvestigatorRole)
    ("PI" :GrantPrincipalInvestigatorRole vivo:PrincipalInvestigatorRole )
    ("CLDR" :GrantCoreLeaderRole vivo:ResearcherRole)
#    ("ACCT-COPI" :GrantAccountManagerRole vivo:ResearcherRole)
    }
	BIND(URI(CONCAT(str(grant:), ?cgprpsl_nbr)) AS ?grant)
  BIND(md5(concat(?prncpl_nm,'@ucdavis.edu')) AS ?user_id)
	BIND(URI(CONCAT(str(?grant), "#role-",?user_id,"-",?grant_role)) AS ?role)
	BIND(URI(CONCAT(str(expert:), ?user_id)) as ?expert)
  BIND(CONCAT(?expert_nm, " (", ?grant_role, ")") AS ?role_label)
}
