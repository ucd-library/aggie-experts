# See https://wiki.lyrasis.org/download/attachments/112526814/Grant%20Model.png?version=1&modificationDate=1553028250086&api=v2
PREFIX : <ark:/87287/d7gt0q/schema#>
PREFIX experts: <http://experts.ucdavis.edu/>
PREFIX grant: <ark:/87287/d7gt0q/grant/>
PREFIX kfs: <ark:/87287/d7gt0q/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX expert: <http://experts.ucdavis.edu/expert/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ucdlib: <http://schema.library.ucdavis.edu/schema#>
PREFIX vivo: <http://vivoweb.org/ontology/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
	?grant a ucdlib:Grant,vivo:Grant,?grant_class;
	      rdfs:label ?title;
	     vivo:sponsorAwardId ?grantor_award_id;
 	    vivo:totalAwardAmount ?grant_amount_i;
 	    vivo:assignedBy ?funding_org;
 	    vivo:relates ?admin_role;
	    vivo:dateTimeInterval ?grant_duration;
     vivo:grantDirectCosts ?direct_costs_i;
     ucdlib:subAwardOf ?from_grant;
 	   ucdlib:grantIndirectCosts ?indirect_costs_i;
     ucdlib:flowThruFunding ?fpt_funding_org;
     ucdlib:piName ?person_nm;
     .

  ?grant_duration a vivo:DateTimeInterval;
                  vivo:start ?grant_start;
                  vivo:end ?grant_end;
                  .

  ?grant_start a vivo:DateTimeValue;
               vivo:dateTime ?start_date;
               vivo:dateTimePrecision vivo:yearMonthDayPrecision;
               .

  ?grant_end a vivo:DateTimeValue;
             vivo:dateTime ?end_date;
             vivo:dateTimePrecision vivo:yearMonthDayPrecision;
             .

	?funding_org vivo:assigns ?grant.

  ?from_grant a vivo:Grant;
 	            vivo:assignedBy ?fpt_funding_org;
              .

  ?fpt_funding_org vivo:assigns ?from_grant.

  ?admin_role a vivo:AdminRole;
	            vivo:relatedBy ?grant;
             obo:RO_0000052 ?admin_department;
             rdfs:label ?admin_dept_nm;
             .
}
WHERE {
  VALUES (?grant_type ?grant_class) {
    ("INSTRUCTION" :GrantTypeInstruction)
    ("RESEARCH" :GrantTypeResearch)
    ("SERVICE/OTHER" :GrantTypeService)
    ("ACADEMIC SUPPORT" :GrantTypeAcademicSupport)
    ("STUDENT SERVICES" :GrantTypeStudentService)
    ("SCHOLARSHIPS / FELLOWSHIPS" :GrantTypeScholarship)
    ("DEFAULT" :GrantTypeDefault)
  }


	BIND(URI(CONCAT(str(kfs:), "admin_role/",?admin_dept_cd, "-", ?cgprpsl_nbr)) as ?admin_role)
	BIND(URI(CONCAT(str(kfs:), "organization/",?admin_dept_cd)) as ?admin_department)

	BIND(URI(CONCAT(str(kfs:), "grant/", ?cgprpsl_nbr)) AS ?grant)
	BIND(URI(CONCAT(str(?grant), "#duration")) AS ?grant_duration)
	BIND(URI(CONCAT(str(?grant), "#start")) AS ?grant_start)
	BIND(URI(CONCAT(str(?grant), "#end")) AS ?grant_end)

  BIND(xsd:integer(coalesce(?grant_amount,?direct_costs+?indirect_costs,?direct_costs,?indirect_costs,0)) AS ?grant_amount_i)
  BIND(xsd:integer(?direct_costs) AS ?direct_costs_i)
  BIND(xsd:integer(?indirect_costs) AS ?indirect_costs_i)

  BIND(md5(concat(?prncpl_nm,'@ucdavis.edu')) AS ?user_id)
	BIND(URI(CONCAT(str(experts:), "expert/", ?user_id)) as ?expert)
	BIND(URI(CONCAT(str(kfs:), "funding_org/", ?agency_nbr)) as ?funding_org)

	BIND(URI(CONCAT(str(?grant), "#subAwardOf_", md5(?fpt_agency_nbr))) as ?from_grant)
	BIND(URI(CONCAT(str(kfs:), "funding_org/", ?fpt_agency_nbr)) as ?fpt_funding_org)
}
