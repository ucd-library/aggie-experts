PREFIX experts: <http://experts.ucdavis.edu/>
PREFIX harvest_iam: <http://iam.ucdavis.edu/>
PREFIX iam: <http://iam.ucdavis.edu/schema#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX expert: <http://experts.ucdavis.edu/expert/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX ucdlib: <http://schema.library.ucdavis.edu/schema#>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>
PREFIX vivo: <http://vivoweb.org/ontology/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
  ?user a ?emp_type,ucdlib:Expert;
        rdfs:label ?label;
        schema:identifier ?aggie_ark;
        ucdlib:userId ?kerb;
        ucdlib:is-visible ?is_visible;
        .

  ?user a ?emp_type;
        vcard:hasName ?vcard_name;
        .

  ?vcard_name a vcard:Name;
                 vcard:givenName ?fname;
                 vcard:middleName ?mname;
                 vcard:familyName ?lname;
                 vcard:pronouns ?pronoun;
                 .

  ?user obo:ARG_2000028 ?vcard.
  ?vcard a vcard:Individual;
         vivo:rank ?order;
         vcard:hasName ?vcard_name;
         vcard:hasTitle ?vcard_title;
         vcard:hasOrganizationalUnit ?vcard_unit;
         vcard:hasEmail ?email_uri;
         vcard:hasURL ?vcard_url;
         ucdlib:isPreferred ?is_preferred;
         .

  ?vcard_title a vcard:Title;
               vcard:title ?title;
               .

  ?vcard_unit a vcard:Organization;
              vcard:title ?dept;
              .

  ?vcard_url a vcard:URL;
             vcard:url ?website;
             ucdlib:urlType ucdlib:URLType_other;
             .
}
WHERE {
  bind(?userId_binding as ?kerb)
  graph harvest_iam: {
    ?s iam:email ?default_email;
       iam:userID ?kerb;
       iam:employeeId ?employee_id;
       iam:dLastName ?iam_lname;
       iam:dFirstName ?iam_fname;
       iam:isFaculty ?faculty;
       .

    OPTIONAL { ?s iam:dMiddleName ?iam_mname . }
    OPTIONAL {
      ?s iam:directory/iam:displayName/iam:nameWwwFlag "N" .
      bind(false as ?odr_is_visible)
    }
    OPTIONAL {
      ?s iam:directory/iam:displayName ?dname.
      ?dname iam:nameWwwFlag "Y";
             iam:preferredFname ?better_fname;
             iam:preferredLname ?better_lname;
             .
      OPTIONAL {
        ?dname iam:preferredMname ?better_mname .
        bind(concat(?better_fname," ",?better_mname," ",?better_lname) as ?better_mlabel)
      }
      OPTIONAL {
        ?dname iam:preferredPronouns ?pronoun .
      }
    }
    bind(coalesce(?better_fname,?iam_fname) as ?fname)
    bind(coalesce(?better_lname,?iam_lname) as ?lname)
    bind(?better_mname as ?mname)
    bind(coalesce(?better_mlabel,concat(?fname," ",?lname)) as ?label)
    bind(coalesce(?odr_is_visible,true) as ?is_visible)

    bind(concat(?kerb,"@ucdavis.edu") as ?username)
    bind(md5(?username) as ?user_id)
    bind(uri(concat(str(expert:),?user_id)) as ?user)
    bind(uri(concat('ark:/87287/d7c08j/',md5(concat('employeeId/',?employee_id)))) as ?aggie_ark)
    bind(if(?faculty=true,vivo:FacultyMember,vivo:NonAcademic) as ?emp_type)
    bind(uri(concat(str(?user),"#name")) as ?vcard_name)
  }
  OPTIONAL {
    {
      select ?s ?vid ?vcard_unit ?title ?dept ?order ?website ?title_email ?is_preferred
      WHERE {
        graph harvest_iam: {
          ?s iam:directory ?dir .
          ?dir iam:listings ?list.
          OPTIONAL {
            ?list iam:deptWwwFlag "Y";
                  iam:listingOrder ?order;
                  iam:deptName ?dept;
                  iam:deptCode ?deptCode;
                  .
          }
          OPTIONAL {
            ?list iam:titleWwwFlag "Y";
                  iam:title ?title;
                  .
          }
          OPTIONAL {
            ?list iam:websiteWwwFlag "Y";
                  iam:website ?website;
                  .
          }
          OPTIONAL {
            ?list iam:emailWwwFlag "Y";
                  iam:email ?title_email;
                  .
          }
          bind(concat("odr-",str(?order)) as ?vid)
          bind(true as ?is_preferred)
        }
        bind(uri(concat(str(experts:),"dept/odr/",?deptCode)) as ?vcard_unit)
      }
    }
    UNION {
      select ?s ?vid ?vcard_unit ?ucd_title ?title ?dept ?order ?use_default_email ?is_preferred
      where {
        graph harvest_iam: {
          ?s iam:ppsAssociations [iam:assocRank ?a_order;
                                 iam:titleOfficialName ?otitle;
                                 iam:titleCode ?titleCode;
                                 iam:deptCode ?deptCode;
                                 iam:deptOfficialName ?dept ];
                                                      .
          OPTIONAL {
            ?s iam:directory/iam:listings/iam:title [].
            bind(true as ?has_odr)
          }
          bind(replace(?otitle," -.*","") as ?title)
          bind(xsd:integer(?a_order)+10 as ?order)
          bind(concat("pps-",str(?a_order)) as ?vid)
          bind(true as ?use_default_email)
          bind(coalesce((! ?has_odr),true) as ?is_preferred)
          bind(uri(concat(str(experts:),"dept/pps/",?deptCode)) as ?vcard_unit)
          bind(uri(concat(str(experts:),"title/pps/",?titleCode)) as ?ucd_title)
        }
      }
    }
  }
  bind(uri(concat(str(?user),"#vcard-",?vid)) as ?vcard)
  bind(if(bound(?title_email),?title_email,if(bound(?use_default_email),?default_email,?undefined_var)) as ?email)
  bind(if(bound(?email),uri(concat("mailto:",?email)),?undefined_var) as ?email_uri)
  bind(coalesce(?ucd_title,uri(concat(str(?vcard),"-title"))) as ?vcard_title)
  bind(if(bound(?website),uri(concat(str(?vcard),"-url")),?undefined_var) as ?vcard_url)
}
