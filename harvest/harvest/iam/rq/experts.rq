PREFIX experts: <http://experts.ucdavis.edu/>
PREFIX harvest_iam: <http://iam.ucdavis.edu/>
PREFIX iam: <ark:/87287/d7c08j/schema#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX expert: <http://experts.ucdavis.edu/expert/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX ucdlib: <http://schema.library.ucdavis.edu/schema#>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>
PREFIX vivo: <http://vivoweb.org/ontology/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
  ?user a ?emp_type,ucdlib:Expert;
        rdfs:label ?label;
        schema:identifier ?user;
        ucdlib:employeeId ?employee_id;
        ucdlib:userId ?kerb;
        ucdlib:eduroam ?eduroam_email;
        ucdlib:ucdPersonUUId ?mothra_id;
        ucdlib:email ?default_email_uri;
        ucdlib:is-visible ?is_visible;
        .

  ?user a ?emp_type;
        vcard:hasName ?vcard_name;
        .

  ?vcard_name a vcard:Name;
                 vcard:givenName ?fname;
                 vcard:middleName ?mname;
                 vcard:familyName ?lname;
                 vcard:pronouns ?pronoun;
                 .

  ?user obo:ARG_2000028 ?pps_vcard,?odr_vcard.

  ?pps_vcard a vcard:Individual;
         vivo:rank ?pps_order;
         vcard:hasEmail ?default_email_uri;
         vcard:hasOrganizationalUnit ?pps_vcard_unit;
         vcard:hasName ?vcard_name;
         vcard:hasTitle ?pps_title_uri;
         ucdlib:isPreferred ?pps_is_preferred;
         .

  ?pps_title_uri a vcard:Title;
               vcard:title ?pps_title;
               .

  ?pps_vcard_unit a vcard:Organization;
              vcard:title ?pps_dept;
              .

  ?odr_vcard a vcard:Individual;
         vivo:rank ?odr_order;
         vcard:hasEmail ?odr_email_uri;
         vcard:hasOrganizationalUnit ?odr_vcard_unit;
         vcard:hasName ?vcard_name;
         vcard:hasTitle ?odr_vcard_title;
         vcard:hasURL ?odr_vcard_url;
         ucdlib:isPreferred ?odr_is_preferred;
         .

  ?odr_vcard_url a vcard:URL;
                 vcard:url ?odr_website;
                 .

  ?odr_vcard_title a vcard:Title;
               vcard:title ?odr_title;
               .

  ?odr_vcard_unit a vcard:Organization;
              vcard:title ?odr_dept;
              .
}
WHERE {
  bind(?userId_binding as ?kerb)
  graph harvest_iam: {
    ?user iam:email ?default_email;
       iam:userID ?kerb;
       iam:mothraId ?mothra_id;
       iam:employeeId ?employee_id;
       iam:dLastName ?iam_lname;
       iam:dFirstName ?iam_fname;
       iam:isFaculty ?faculty;
       .

    ?user iam:ppsAssociations [iam:assocRank ?pps_a_order;
                              iam:titleOfficialName ?pps_otitle;
                              iam:titleCode ?pps_title_uri;
                              iam:deptCode ?pps_dept_code;
                              iam:deptOfficialName ?pps_dept ];
                                                   .
    OPTIONAL { ?user iam:dMiddleName ?iam_mname . }
    OPTIONAL {
      ?user iam:directory/iam:displayName/iam:nameWwwFlag "N" .
      bind(false as ?odr_is_visible)
    }
    OPTIONAL {
      ?user iam:directory/iam:displayName ?dname.
      ?dname iam:nameWwwFlag "Y";
             iam:preferredFname ?better_fname;
             iam:preferredLname ?better_lname;
             .
      OPTIONAL {
        ?dname iam:preferredMname ?better_mname .
        bind(concat(?better_fname," ",?better_mname," ",?better_lname) as ?better_mlabel)
      }
      OPTIONAL {
        ?dname iam:preferredPronouns ?pronoun .
      }
    }

    OPTIONAL {
      ?user iam:directory/iam:listings ?list.
      ?list iam:listingOrder ?odr_list_order;
            iam:deptWwwFlag "Y";
            iam:deptName ?odr_dept;
            iam:deptCode ?odr_dept_code;
            iam:titleWwwFlag "Y";
            iam:title ?odr_title;
            .

      OPTIONAL {
        ?list iam:websiteWwwFlag "Y";
              iam:website ?odr_website;
              .
      }
      OPTIONAL {
        ?list iam:emailWwwFlag "Y";
              iam:email ?odr_title_email;
              .
      }
      bind(true as ?has_odr)
    }
  }
  # identifiers
  bind(concat(?kerb,"@ucdavis.edu") as ?eduroam_email)
  # ODR Name
  bind(coalesce(?better_fname,?iam_fname) as ?fname)
  bind(coalesce(?better_lname,?iam_lname) as ?lname)
  bind(?better_mname as ?mname)
  bind(coalesce(?better_mlabel,concat(?fname," ",?lname)) as ?label)
  bind(coalesce(?odr_is_visible,true) as ?is_visible)
  bind(if(?faculty=true,vivo:FacultyMember,vivo:NonAcademic) as ?emp_type)
  bind(uri(concat(str(?user),"#name")) as ?vcard_name)
  bind(uri(concat('mailto:',?default_email)) as ?default_email_uri)
  #PPS
  bind(replace(?pps_otitle," -.*","") as ?pps_title)
  bind(xsd:integer(?pps_a_order)+10 as ?pps_order)
  bind(concat("pps-",str(?pps_order)) as ?pps_vid)
  bind(coalesce((! ?has_odr),true) as ?pps_is_preferred)
  bind(uri(concat("ark:/87287/d7c08j/dept/",?pps_dept_code)) as ?pps_vcard_unit)
  bind(uri(concat(str(?user),"#",?pps_vid)) as ?pps_vcard)
  # odr
  bind(xsd:integer(?odr_list_order) as ?odr_order)
  bind(concat("odr-",str(?odr_order)) as ?odr_vid)
  bind(uri(concat("mailto:",?odr_email)) as ?odr_email_uri)
  bind(coalesce(?has_odr,false) as ?odr_is_preferred)
  bind(uri(?odr_website) as ?odr_website_uri)
  bind(uri(concat("ark:/87287/d7c08j/position/odr/",md5(?odr_title))) as ?odr_vcard_title)
  bind(uri(concat("ark:/87287/d7c08j/dept/odr/",?odr_dept_code)) as ?odr_vcard_unit)
  bind(uri(concat(str(?user),"#",?odr_vid)) as ?odr_vcard)
  bind(if(bound(?odr_website),uri(concat(str(?odr_vcard),"-url")),?undefined_var) as ?odr_vcard_url)
}
