# Query for grant roles
PREFIX : <ark:/87287/d7gt0q/schema#>
PREFIX ae: <ark:/87287/d7c08j/>
PREFIX experts: <http://experts.ucdavis.edu/>
PREFIX grant: <ark:/87287/d7gt0q/grant/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ucdlib: <http://schema.library.ucdavis.edu/schema#>
PREFIX vivo: <http://vivoweb.org/ontology/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX schema: <http://schema.org/>
PREFIX cdl: <http://oapolicy.universityofcalifornia.edu/>

select ("grant" as ?category)
(?grant as ?id)
("researchers" as ?field_name)
?surname ?first_name ?full_name
WHERE {
    values (?role_role ?role_type ?link_type_id ?e_role) {
          ("Co-Principal Investigator" vivo:CoPrincipalInvestigatorRole "121" "Co-PI of")
          ("Principal Investigator" vivo:PrincipalInvestigatorRole "120" "Primary investigator for")
        }

  GRAPH ae: {
    ?grant a vivo:Grant;
           vivo:relates ?related
    .
   	?related a ?role_type;
             rdfs:label ?full_name_and_role;
             .
  }

  bind(replace(?full_name_and_role,"\\(.*\\)" ,"") as ?temp)
  bind(replace(STRBEFORE(?temp," "),"," ,"") as ?surname)
  bind(replace(STRAFTER(?temp," "),"," ,"") as ?first_name)
  bind(?temp as ?full_name)
} order by ?id ?surname ?first_name
