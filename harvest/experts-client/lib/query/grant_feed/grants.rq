# Query for grants
PREFIX : <ark:/87287/d7gt0q/schema#>
PREFIX ae: <ark:/87287/d7c08j/>
PREFIX iam: <http://iam.ucdavis.edu/>
PREFIX experts: <http://experts.ucdavis.edu/>
PREFIX grant: <ark:/87287/d7gt0q/grant/>
PREFIX ae:  <ark:/87287/d7c08j/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ucdlib: <http://schema.library.ucdavis.edu/schema#>
PREFIX vivo: <http://vivoweb.org/ontology/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX cdl: <http://oapolicy.universityofcalifornia.edu/>
select distinct
(?grant AS ?id)
("grant" AS ?category)
("c-ucdavis-pre-ae" AS ?type)
?title
?c_pi
?funder_name
?funder_reference
?start_date
?end_date
?amount_value
("USD" as ?amount_currency_code)
?funding_type
?c_ucop_sponsor
?c_flow_thru_funding
("TRUE" AS ?visible)
where {
  GRAPH ae: {
    ?grant a vivo:Grant,?grant_class ;
              rdfs:label ?title;
              ucdlib:piName ?c_pi;
              vivo:totalAwardAmount ?amount_value;
              vivo:dateTimeInterval [
                                      vivo:start/vivo:dateTime ?start_date;
                                      vivo:end/vivo:dateTime ?end_date;
                                    ];
    .

    VALUES (?funding_type ?grant_class) {
       ("Academic Support" ucdlib:GrantAcademicSupport)
                  ("Research" ucdlib:GrantResearchAES)
                  ("Student Services" ucdlib:GrantStudentServices)
                  ("Capital Projects" ucdlib:GrantCapitalProjects)
#                  ("78-Student Financial Aid" ucdlib:GrantStudentFinancialAid)
                  ("Instruction" ucdlib:GrantInstruction)
                  ("Research" ucdlib:GrantResearch)
                  ("Public Service / Other" ucdlib:GrantPublicService)
    }

    OPTIONAL {
      ?grant ucdlib:flowThruFunding ?c_flow_thru_funding.
    }

    OPTIONAL {
      ?grant vivo:assignedBy ?c_ucop_sponsor;
             vivo:sponsorAwardId ?funder_reference;
             .
       ?c_ucop_sponsor rdfs:label ?funder_name.
    }
    # This filters grants without a value oracle_award_number
    filter(! regex(str(?grant),"K-"))
  }
} order by ?id
