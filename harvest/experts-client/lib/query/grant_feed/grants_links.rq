# Query for Grant CDL User links.
# Note that the CDL graph must be created using the experts-cdl-users script before this query can be successful.

PREFIX : <ark:/87287/d7gt0q/schema#>
PREFIX experts: <http://experts.ucdavis.edu/>
PREFIX grant: <ark:/87287/d7gt0q/grant/>
PREFIX kfs: <ark:/87287/d7gt0q/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ucdlib: <http://schema.library.ucdavis.edu/schema#>
PREFIX vivo: <http://vivoweb.org/ontology/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX iam: <http://iam.ucdavis.edu/>
PREFIX cdl: <http://oapolicy.universityofcalifornia.edu/>

select ("user" as ?category_1) (?ucpath as ?id_1)
("grant" as ?category_2) (?grant as ?id_2)
?link_type_id ("TRUE" as ?visible)
WHERE {
  VALUES (?role ?link_type_id ?e_role) {
    #       (:GrantAccountManagerRole "183" "Researcher on" )
    (:GrantOtherRole "138" "Researcher on" )
    (:GrantProjectLeaderRole "118" "Project Lead of")
    (:GrantCoPrincipalInvestigatorRole "121" "Co-PI of")
    (:GrantProgramDirectorRole "137" "Program Director of")
    (:GrantCoreLeaderRole "119" "Co leader on")
    (:GrantKeyPersonnelRole "97" "Senior personal of")
    (:GrantPrincipalInvestigatorRole "120" "Primary investigator for")
  }
  graph kfs: {
    ?grant a vivo:Grant;
           vivo:relates ?related;
           .
    ?related a ?role ;
    obo:RO_0000052 ?expert;
    .
    # This filters grants without a value oracle_award_number
    filter(! regex(str(?grant),"K-"))
  }

  GRAPH cdl: {
	  ?expert ucdlib:proprietary_id ?ucpath .
  }
} order by ?id_2 ?id_1
