PREFIX : <http://oapolicy.universityofcalifornia.edu/vocab#>
PREFIX cdl: <http://oapolicy.universityofcalifornia.edu/>
PREFIX experts: <http://experts.ucdavis.edu/>
PREFIX expert: <http://experts.ucdavis.edu/expert/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ucdlib: <http://schema.library.ucdavis.edu/schema#>
PREFIX vivo: <http://vivoweb.org/ontology/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX kfs: <ark:/87287/d7gt0q/schema#>

construct {
  ?grant_role a ucdlib:GrantRole, ?role, ?vivo_role;
           ucdlib:favorite ?favorite;
           ucdlib:is-visible ?visible;
#           ucdlib:type_id ?tid;
#           ucdlib:type ?type;
           vivo:relates ?expert,?grant;
           vivo:rank ?user_rank;
           .

  ?grant a ucdlib:Grant,vivo:Grant;
         ?field_predicate ?field_text;
         ?field_predicate ?field_money;
         ucdlib:GrantType ?grant_type;
         vivo:assignedBy ?funder;
         vivo:dateTimeInterval ?interval;
         .

  ?grant_type a vivo:GrantType;
              rdfs:label ?funding_type_label;
              .

  ?funder a vivo:FundingOrganization;
          rdfs:label ?funder_label;
          .

  ?interval a vivo:DateTimeInterval;
            ?field_predicate ?field_date;
            .

  ?field_date a vivo:DateTimeValue;
              vivo:dateTimePrecision ?precision;
              vivo:dateTime ?field_datestr;
              .
}
WHERE {

  graph cdl: {
    ?RELATIONSHIP__
    :is-visible ?visible_str;
    :type ?type;
    :type-id ?tid;
    :related ?g;
    :related ?user;
    .

    OPTIONAL {
	    ?RELATIONSHIP__ :is-favourite "true".
    	bind(true as ?favorite)
    }

    OPTIONAL {
           VALUES (?role ?tid ?vivo_role ) {
#       (:GrantOtherRole "138" "Researcher on" )
#       (:GrantProjectLeaderRole "118" "Project Lead of")
       (kfs:GrantCoPrincipalInvestigatorRole "116" vivo:CoPrincipalInvestigatorRole)
#       (:GrantProgramDirectorRole "137" "Program Director of")
#       (:GrantCoreLeaderRole "119" "Co leader on")
#       (:GrantKeyPersonnelRole "97" "Senior personal of")
       (kfs:GrantPrincipalInvestigatorRole "43" vivo:PrincipalInvestigatorRole)
     }

#        VALUES (?grant_role ?ucd_role ?vivo_role) {
#    ("PDIR" kfs:GrantProgramDirectorRole vivo:LeaderRole)
#    ("KP" kfs:GrantKeyPersonnelRole vivo:ResearcherRole)
##    ("OTHR" kfs:GrantOtherRole vivo:ResearcherRole)
#    ("PLDR" kfs:GrantProjectLeaderRole vivo:LeaderRole)
#    ("COPI" kfs:GrantCoPrincipalInvestigatorRole vivo:CoPrincipalInvestigatorRole)
#    ("PI" kfs:GrantPrincipalInvestigatorRole vivo:PrincipalInvestigatorRole )
#    ("CLDR" kfs:GrantCoreLeaderRole vivo:ResearcherRole)
##    ("ACCT-COPI" kfs:GrantAccountManagerRole vivo:ResearcherRole)
#    }

    }
    bind(xsd:boolean(?visible_str) as ?visible)
    ?user :category "user";
          :username ?username;
          .

    # This needs to include grants later
    ?g :category "grant";
       .

    # We only use one source for grants
    {
      select ?g (min(?id) as ?source_id)
      WHERE {
        ?g :records/:record ?record.
        ?record :source-name  ?source;
                :source-id ?id;
                .
      }  group by ?g
    }
    # Now get the corresponding ?record w/ that low score,
    # I'm calling it field_record even though it's currently the same for all
 	  ?g :records/:record ?record.
    ?field_record :source-name  ?source;
                  :source-id ?wanted_source_id;
                  .

    # these are the fields to check
    VALUES(?field_name ?field_type ?field_predicate) {
      ("title" "text" rdfs:label)
      ("amount" "money" vivo:totalAwardAmount)
      ("start-date" "date" vivo:start)
      ("end-date" "date" vivo:end)
      ("funder-reference" "text" vivo:sponsorAwardId)
      ("funding-type" "special" ucdlib:none)
      ("funder-name" "special" ucdlib:none)
    }

    ?g :type ?oap_type;
       :last-modified-when ?lastModifiedWhen;
       :records/:record ?field_record;
       .

    ?field_record :native/:field ?field.

    OPTIONAL {
      ?field :name ?field_name;
             :type "text";
             :type ?field_type ;
      :text ?field_text ;
      .
    }
    OPTIONAL {
      ?field :name ?field_name;
             :type "money";
             :type ?field_type;
             :money/:field-value ?field_money;
             .
    }

    # Funding type is a special case
    OPTIONAL {
      values (?grant_type ?funding_type_label) {
        (kfs:GrantTypeAcademicSupport "Academic Support" )
        (kfs:GrantTypeDefault "Default" )
        (kfs:GrantTypeInstruction "Instruction" )
        (kfs:GrantTypeResearch "Research" )
        (kfs:GrantTypeService "Service / Other" )
        (kfs:GrantTypeScholarship "Scholarships / Fellowships" )
        (kfs:GrantTypeStudentService "Student Services" )
      }
      ?field :name "funding-type";
             :type "text";
             :text ?funding_type_label;
             .
    }

    # Funder name is a special case
    OPTIONAL {
      ?field :name "funder-name";
             :type "text";
             :text ?funder_label;
             .
      bind('unknown-funder' as ?funder_ref)
    }

    OPTIONAL {
      ?field :name ?field_name;
             :type "date";
             :type ?field_type;
             :date ?date.

      ?date :year ?year.
      BIND(vivo:yearPrecision AS ?year_precision)
      OPTIONAL {
        ?date :month ?m_raw.
        BIND(concat("-",IF(xsd:integer(?m_raw) < 10, CONCAT("0", ?m_raw), ?m_raw)) AS ?month)
        BIND(vivo:yearMonthPrecision AS ?month_precision)
        OPTIONAL {
          ?date :day ?d_raw.
          BIND(concat("-",IF(xsd:integer(?d_raw) < 10, CONCAT("0", ?d_raw), ?d_raw)) AS ?day)
          BIND(vivo:yearMonthDayPrecision AS ?day_precision)
        }
      }
      BIND(CONCAT(?year, COALESCE(?month, ""), COALESCE(?day, "")) AS ?field_datestr)
      BIND('duration' as ?duration)
      BIND(?field_name as ?interval_name)
      BIND(coalesce(?day_precision,?month_precision,?year_precision) as ?precision)
    }
  }
  # EXPERTS ids
  bind(uri(replace(str(?RELATIONSHIP__),str(cdl:),"ark:/87287/d7mh2m/relationship/")) as ?grant_role)
  bind(uri(replace(str(?g),str(cdl:),"ark:/87287/d7mh2m/grant/")) as ?grant)
  bind(uri(concat(str(expert:),md5(?username))) as ?expert)

  # If funder_ref, then we make a special funder until REMS comes
  bind(uri(concat(str(?grant),'#',?funder_ref)) as ?funder)


  bind(uri(concat(str(?grant),'#',?duration)) as ?interval)
  bind(uri(concat(str(?grant),'#',?interval_name)) as ?field_date)
}
