prefix : <http://oapolicy.universityofcalifornia.edu/vocab#>
PREFIX bibo: <http://purl.org/ontology/bibo/>
prefix cdl: <ark:/87287/d7_cdltest_/>
prefix experts: <http://experts.ucdavis.edu/>
prefix oap: <http://oapolicy.universityofcalifornia.edu/>
prefix person: <http://experts.ucdavis.edu/person/>
prefix ucdlib: <http://schema.library.ucdavis.edu/schema#>
prefix vivo: <http://vivoweb.org/ontology/core#>

select distinct (?relationship as ?RELATIONSHIP__) ?filename ?type where {

  graph ?g {
    ?relationship :type ?type;
                  :related ?grant;
                  :related ?user;
                  .
    ?grant :category "grant";
         .
    ?user :category "user";
          :username ?username;
          .
  }

  bind(replace(str(?relationship),str(oap:),"ark:/87287/d7mh2m/relationship/") as ?id)
  filter(regex(?username,"@ucdavis.edu"))
  bind(concat('expert/',md5(?username),"/",?id,".jsonld.json") as ?filename)
}
