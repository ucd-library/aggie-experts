PREFIX : <http://oapolicy.universityofcalifornia.edu/vocab#>
PREFIX FoR: <http://experts.ucdavis.edu/concept/FoR/>
PREFIX expert: <http://experts.ucdavis.edu/expert/>
PREFIX experts: <http://experts.ucdavis.edu/>
PREFIX fedora: <http://fedora.info/definitions/v4/repository#>
PREFIX harvest_oap: <http://oapolicy.universityofcalifornia.edu/>
PREFIX list: <http://jena.apache.org/ARQ/list#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX ucdlib: <http://schema.library.ucdavis.edu/schema#>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>
PREFIX vivo: <http://vivoweb.org/ontology/core#>
PREFIX ucop: <http://experts.ucdavis.edu/ucop/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
  ?EXPERT__ a vivo:Person,ucdlib:Expert,?type;
            rdfs:label ?name;
            vcard:hasName ?vcard_name;
            obo:ARG_2000028 ?vcard,?oap_vcard;
               schema:identifier ?EXPERT__, ?oapolicy_ark;
               ucdlib:is-visible ?visible;
               ?field_predicate ?field_value;
               ?p ?o
               .

  ?EXPERT__ ?id_pred ?id_value.

  ?vcard_name ?np ?no.

    ?vcard vcard:hasOrganizationalUnit ?unit;
    vcard:hasTitle ?position;
    ?vp ?vo;
    .

  ?unit a ?unit_type;
    vcard:title ?unit_title;
    .

  ?position a ?position_type, ?ucop_type;
    vcard:title ?position_title;
    skos:prefLabel ?prefLabel;
    .


  ?oap_vcard a vcard:Individual;
         vivo:rank 20;
         schema:name ?oap_vcard_label;
         vcard:hasName ?oap_vcard_name;
         vcard:hasEmail ?oap_vcard_email;
         vcard:hasURL ?oap_vcard_web;
         ucdlib:isPreferred false;
         .

  ?oap_vcard_name a vcard:Name;
              vcard:givenName ?fn;
              vcard:familyName ?ln;
              .

  ?oap_vcard_web a vcard:URL, ?web_type;
             vcard:url ?web_url;
             vivo:rank ?web_rank;
             vcard:title ?web_label;
             .

  # Research Areas
  ?EXPERT__ vivo:hasResearchArea ?concept.
  ?concept vivo:researchAreaOf ?EXPERT__.
}
WHERE {
  graph <http://iam.ucdavis.edu/> {
    values ?p {rdfs:label schema:identifier schema:name
      ucdlib:is-visible }

    ?user a ?type;
          ucdlib:eduroam ?USERNAME__;
          ucdlib:is-visible true;
          vcard:hasName ?vcard_name;
          obo:ARG_2000028 ?vcard;
          ?p ?o;
          .

	  ?vcard_name ?np ?no.

	  ?vcard ?vp ?vo.
    OPTIONAL {
	    ?vcard vcard:hasOrganizationalUnit ?unit.
    	?unit a ?unit_type;
        	   vcard:title ?unit_title;
           .
    }
    OPTIONAL {
		?vcard vcard:hasTitle ?position.
    	?position a ?position_type;
        		   vcard:title ?position_title;
           .
    }
  }
  OPTIONAL {
    graph ucop: {
      ?position a ?ucop_type;
             skos:prefLabel ?prefLabel;
                .
    }
  }
  GRAPH <http://oapolicy.universityofcalifornia.edu/> {
  ?cdl_id :category "user";
        :is-public "true",?visible_str;
        :is-login-allowed "true";
        :username ?USERNAME__;
        :last-name ?ln;
        :first-name ?fn;
        :user-identifier-associations ?assoc;
        .

    OPTIONAL {
      ?cdl_id :position ?title.
    }
    OPTIONAL {
      ?cdl_id :department ?dept.
    }

    ?assoc :user-id ?oapolicy_id.
    bind(uri(concat('ark:/87287/d7mh2m/user/',?oapolicy_id)) as ?oapolicy_ark)

    OPTIONAL {
      values (?oap_scheme ?id_pred) {
        ("researcherid" vivo:researcherId)
        ("orcid" vivo:orcidId)
        ("scopus-author-id" vivo:scopusId) }
      ?assoc :user-identifier-association [ :field-value  ?id_value ; :scheme ?oap_scheme ].
  }

    OPTIONAL {
      ?cdl_id :records/:record/:native ?native.
      OPTIONAL {
        ?native :field [ :name "email-addresses";
                         :type "email-address-list";
                         :email-addresses [
                                            :email-address [
                                                             :address ?email;
                                                             :privacy "public";
                                                           ]
                                          ]
                       ].
      }
      OPTIONAL {
        ?native :field [ :name "personal-websites";
                         :web-addresses/:web-address [ list:index(?pos ?elem) ]
                       ].

        ?elem :url ?web_url;
              :privacy "public";
              .
        OPTIONAL {
          ?elem :label ?web_label.
        }

        OPTIONAL {
          ?elem :type ?web_type_text.
          bind(uri(concat(str(ucdlib:),"URL_",?web_type_text)) as ?web_type)
        }
        bind(?pos as ?web_rank)
      }
      OPTIONAL {
        values (?field_name ?field_predicate ) {
          ("overview" vivo:overview)
          ("research-interests" ucdlib:researchInterests)
          ("teaching-summary" ucdlib:teachingSummary)
        }
        ?native :field [ :name ?field_name;
                         :text [ :field-value ?field_value;
                                 :privacy "public"; ]
                       ].
        }
    }
    OPTIONAL {
      values (?scheme ?vocab) { ("for" FoR:) }
      ?cdl_id :all-labels [
                            :type "keyword-list";
                            :keywords/:keyword [
                                                 :field-value ?value;
                                                 :scheme ?scheme;
                                               ]
                          ].
      bind(uri(concat(str(?vocab),replace(?value," .*",""))) as ?concept)
    }
  }
  bind(concat(?fn," ",?ln) as ?name)
  bind(uri(concat(str(?EXPERT__),"#vcard-oap-1")) as ?oap_vcard)
  bind(uri(concat(str(?oap_vcard),"-name")) as ?oap_vcard_name)
  bind(xsd:boolean(?visible_str) as ?visible)
  bind(uri(concat('mailto:',str(?email))) as ?oap_vcard_email)
  bind(uri(concat(str(?oap_vcard),"-web-",str(?web_rank))) as ?oap_vcard_web)
  bind(coalesce(concat(" ยง ",?title,coalesce(concat(", ",?dept),"")),
                "") as ?title_dept)
  bind(concat(
              concat(?ln,coalesce(concat(", ",?fn),"")),
              coalesce(?title_dept,"")
              ) as ?oap_vcard_label)
}
