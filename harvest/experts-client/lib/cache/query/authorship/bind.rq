PREFIX : <ark:/87287/d7mh2m/schema#>
prefix experts: <http://experts.ucdavis.edu/>
PREFIX oap: <ark:/87287/d7mh2m/schema#>
prefix expert: <http://experts.ucdavis.edu/expert/>
PREFIX schema: <http://schema.org/>
prefix ucdlib: <http://schema.library.ucdavis.edu/schema#>
PREFIX iam: <ark:/87287/d7c08j/schema#>
prefix vivo: <http://vivoweb.org/ontology/core#>

select distinct ?filename ?EXPERT__ ?RELATIONSHIP__
where {
  VALUES(?oap_type ?cite_include ?schema_type ?cite_type ?cite_genre) {
    ("book" true schema:Book "book" undef)
    ("chapter" true schema:Chapter "chapter" undef)
    ("conference" true schema:ScholarlyArticle "paper-conference" undef)
    ("journal-article" true schema:ScholarlyArticle "article-journal" undef)
      # false
      #("dataset" false ucdlib:Work "dataset" "")
      #("internet-publication" false ucdlib:Work "webpage" "")
      #("media" false ucdlib:Work "article" "media")
      #("other" false ucdlib:Work "article" "other")
      #("poster" false ucdlib:Work "speech" "poster")
      #("preprint" false ucdlib:Preprint "article" "preprint" )
      #("presentation" false ucdlib:Work "speech" "presentation")
      #("report" false ucdlib:Work "report" "")
      #("scholarly-edition" false ucdlib:Work "manuscript" "scholarly-edition")
      #("software" false ucdlib:Work "software" "")
      #("thesis-dissertation" false ucdlib:Work "thesis" "dissertation")
    }
  graph <ark:/87287/d7mh2m/> {
    ?RELATIONSHIP__ :type "publication-user-authorship";
#                  :effective-privacy-level "Public";
#                  :is-visible "true";
                  :related ?pub;
                  :related ?user;
                  .
    ?pub :category "publication";
         :type ?oap_type;
         .
    ?user :category "user";
          :username ?username;
          :is-public "true";
          .
  }
  graph <ark:/87287/d7c08j/> {
    [] iam:userID ?USERID__;
          iam:email ?EMAIL__;
          iam:expertId ?EXPERTID__;
          iam:directory/iam:displayName/iam:nameWwwFlag "Y" ;
          .
  }
  bind(iri(concat("http://experts.ucdavis.edu/expert/",?EXPERTID__)) as ?EXPERT__)
  bind(replace(str(?RELATIONSHIP__),str(oap:),"ark:/87287/d7mh2m/relationship/") as ?id)
  bind(concat(?EXPERTID__,"/",?id,".jsonld.json") as ?filename)
}
