PREFIX : <ark:/87287/d7mh2m/schema#>
PREFIX cite: <http://citationstyles.org/schema/>
PREFIX expert: <http://experts.ucdavis.edu/expert/>
PREFIX experts: <http://experts.ucdavis.edu/>
PREFIX js: <http://jena.apache.org/ARQ/jsFunction#>
PREFIX kfs: <ark:/87287/d7gt0q/schema#>
PREFIX list: <http://jena.apache.org/ARQ/list#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX ucdlib: <http://schema.library.ucdavis.edu/schema#>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>
PREFIX venue: <http://experts.ucdavis.edu/venue/>
PREFIX vivo: <http://vivoweb.org/ontology/core#>
PREFIX work: <http://experts.ucdavis.edu/work/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
construct {
  ?RELATIONSHIP__ a ucdlib:GrantRole, ?role;
		    schema:name ?role_name;
                ucdlib:favorite ?favorite;
                ucdlib:is-visible ?visible;
                vivo:relates ?grant,?EXPERT__;
                obo:RO_0000052 ?EXPERT__;
                .

  ?grant a vivo:Grant,?grant_type;
         schema:name ?grant_label;
         schema:identifier ?grant,?g;
         cite:status ?status;
         vivo:totalAwardAmount ?total_award_amount;
         vivo:sponsorAwardId ?sponsor_award_id;
         vivo:assignedBy ?funder;
         vivo:dateTimeInterval ?interval;
         vivo:relatedBy ?RELATIONSHIP__,?labeled_only_role,?pi_labeled_only_role;
         .

  ?pi_labeled_only_role a vivo:PrincipalInvestigatorRole;
			schema:name ?pi_labeled_only_role_name;
                        vivo:relates ?pi_labeled_only_person,?grant;
                           .

  ?pi_labeled_only_person
                       schema:name ?pi_labeled_only_name;
                       vcard:hasName ?pi_labeled_only_person_vcard;
                       .

  ?pi_labeled_only_person_vcard
                       vcard:familyName ?pi_labeled_only_name_last;
                       vcard:givenName ?pi_labeled_only_name_first;
                       .


  ?labeled_only_role a vivo:CoPrincipalInvestigatorRole;
		     schema:name ?labeled_only_role_name;
                     vivo:relates ?labeled_only_person,?grant;
                           .

  ?labeled_only_person
                       schema:name ?labeled_only_name;
                       vcard:hasName ?labeled_only_person_vcard;
                       .

  ?labeled_only_person_vcard
                       vcard:familyName ?labeled_only_name_last;
                       vcard:givenName ?labeled_only_name_first;
                       vcard:initials ?labeled_only_initials;
                      .

  ?funder a vivo:FundingOrganization;
          schema:name ?funder_label;
          .

  ?interval vivo:start ?sd;
            vivo:end ?ed;
            .

  ?sd vivo:dateTimePrecision ?start_date_precision;
      vivo:dateTime ?start_date;
      .
  ?ed vivo:dateTimePrecision ?end_date_precision;
      vivo:dateTime ?end_date;
      .
}
WHERE {
  graph <ark:/87287/d7mh2m/> {
    ?RELATIONSHIP__
    :is-visible ?visible_str;
    :type ?cdl_type;
    :related ?g;
    :related ?user;
    .

    OPTIONAL {
      VALUES (?cdl_type ?vivo_role ?vivo_role_abbrev) {
        ("user-grant-principal-investigation" vivo:PrincipalInvestigatorRole "PI")
        ("user-grant-co-principal-investigation" vivo:CoPrincipalInvestigatorRole "CoPI")
        ("user-grant-senior-key-personnel" vivo:ResearcherRole "Res")
        ("user-grant-co-primary-investigation" vivo:CoPrincipalInvestigatorRole "CoPI")
        ("user-grant-primary-investigation" vivo:PrincipalInvestigatorRole "PI")
        ("user-grant-program-direction" vivo:LeaderRole "Lead")
        ("user-grant-project-leadership" vivo:LeaderRole "Lead")
        ("user-grant-research" vivo:ResearcherRole "Res")
      }
    }

    OPTIONAL {
      ?RELATIONSHIP__ :is-favourite "true".
      bind(true as ?favorite)
    }

    ?user :category "user";
          :is-public "true";
          :username ?username;
          :last-name ?user_name_last;
          .

    OPTIONAL {
      ?user :first-name ?user_name_first .
    }
    OPTIONAL {
      ?user :initials ?user_name_initials .
    }

    {
      select ?g ?relationship ?grant_id ?native
      WHERE {
        {
          select ?relationship ?g (min(?id) as ?wanted_source_id)
          WHERE {
            bind(?RELATIONSHIP__ as ?relationship)
            graph <ark:/87287/d7mh2m/> {
              ?RELATIONSHIP__ :related ?g.

              ?g :category "grant";
                 :records/:record/:source-id ?id;
                 .
            }
          }  group by ?relationship ?g
        }
        ?g :records/:record ?record;
           .
        ?record :source-name  ?source;
                :source-id ?wanted_source_id;
                :id-at-source ?grant_id;
                :native ?native;
                .
      }
    }

    OPTIONAL {
      ?native :field [ :name "title";
                               :type "text";
                               :text ?grant_raw_title ];
                                     .
      bind(replace(?grant_raw_title,"(?:SEE\\s+)?(?:(?:[ABCKKXYZ][0-9CF]{6})*(?:\\s*-)?\\s*)*\\s*(?:SP0A\\d{6})?(..*?)(\\s+K.[0-9]{2}.[0-9]{1,2})?$","$1") as ?grant_title)
    }

    OPTIONAL {
      ?native :field [ :name "funder-name";
                               :type "text";
                               :text ?funder_label_cap ].

      ?native :field [ :name "funder-reference";
                               :type "text";
                               :text ?sponsor_award_id ].
    }

    OPTIONAL {
      ?native :field [ :name "amount";
                               :type "money";
                               :money/:field-value ?total_award_amount ];
                                                   .
    }

    OPTIONAL {
      ?native :field [ :name "c-pi";
                               :type "text";
                               :text ?pi_name_cap ];
                                     .
    }

    OPTIONAL {
      ?native :field [ :name "c-co-pis";
                               :people/:person [ list:index(?pos ?elem) ]
                             ].
      ?elem :last-name ?labeled_only_name_last_cap .
      bind(js:capitalizeName(?labeled_only_name_last_cap) as ?labeled_only_name_last)
      OPTIONAL {
        ?elem :first-names ?labeled_only_name_first_cap .
        bind(js:capitalizeName(?labeled_only_name_first_cap) as ?labeled_only_name_first)
      }
      OPTIONAL {
        ?elem :initials ?labeled_only_name_initials .
      }
    }

    OPTIONAL {
      values (?grant_type ?funding_type_label) {
        (ucdlib:Grant_AcademicSupport "Academic Support" )
        (ucdlib:Grant_Default "Default" )
        (ucdlib:Grant_Instruction "Instruction" )
        (ucdlib:Grant_Research "Research" )
        (ucdlib:Grant_Service "Public Service / Other" )
        (ucdlib:Grant_Scholarship "Scholarships / Fellowships" )
        (ucdlib:Grant_StudentService "Student Services" )
      }
      ?native :field [ :name "funding-type";
			       :type "text";
          :text ?funding_type_label ];
		.
    }


    {
      select ?g ?start_date ?start_year ?start_date_precision ?end_year ?end_date ?end_date_precision
      WHERE {
        {
          select ?relationship ?grant_id ?native
          WHERE {
            {
              select ?relationship ?g (min(?id) as ?wanted_source_id)
              WHERE {
                bind(?RELATIONSHIP__ as ?relationship)
                graph <ark:/87287/d7mh2m/> {
                  ?RELATIONSHIP__ :related ?g.

                  ?g :category "grant";
                     :records/:record/:source-id ?id;
                     .
                }
              }  group by ?relationship ?g
            }
            ?g :records/:record ?record;
               .
            ?record :source-name  ?source;
                    :source-id ?wanted_source_id;
                    :id-at-source ?grant_id;
                    :native ?native;
                    .
          }
        }
        ?native :field ?s;
                :field ?e;
                .

        ?s :name "start-date";
           :type "date";
           :date ?s_date;
           .
        ?s_date :year ?start_year.

        BIND(vivo:yearPrecision AS ?syear_p)
        OPTIONAL {
          ?s_date :month ?sm_raw.
          BIND(concat("-",IF(strlen(?sm_raw) = 1,
                             CONCAT("0", ?sm_raw), ?sm_raw)) AS ?smonth)
          BIND(vivo:yearMonthPrecision AS ?smonth_p)
          OPTIONAL {
            ?s_date :day ?sd_raw.
            BIND(concat("-",IF(strlen(?sd_raw) =1,
                               CONCAT("0", ?sd_raw), ?sd_raw)) AS ?sday)
            BIND(vivo:yearMonthDayPrecision AS ?sday_p)
          }
        }
        BIND(CONCAT(?start_year, COALESCE(?smonth, ""), COALESCE(?sday, "")) AS ?start_date)
        BIND(coalesce(?sday_p,?smonth_p,?syear_p) as ?start_date_precision)

        ?e :name "end-date";
           :type "date";
           :date ?e_date;
           .
        ?e_date :year ?end_year.

        BIND(vivo:yearPrecision AS ?eyear_p)
        OPTIONAL {
          ?e_date :month ?em_raw.
          BIND(concat("-",IF(strlen(?em_raw) = 1,
                             CONCAT("0", ?em_raw), ?em_raw)) AS ?emonth)
          BIND(vivo:yearMonthPrecision AS ?emonth_p)
          OPTIONAL {
            ?e_date :day ?ed_raw.
            BIND(concat("-",IF(strlen(?ed_raw) =1,
                               CONCAT("0", ?ed_raw), ?ed_raw)) AS ?eday)
            BIND(vivo:yearMonthDayPrecision AS ?eday_p)
          }
        }
        BIND(CONCAT(?end_year, COALESCE(?emonth, ""), COALESCE(?eday, "")) AS ?end_date)
        BIND(coalesce(?eday_p,?emonth_p,?eyear_p) as ?end_date_precision)

      }
    }
  }

  bind(uri(if(strstarts(?grant_id,"ark:"),?grant_id,concat("ark:/87287/d7mh2m/grant/",?grant_id))) as ?grant)

  bind(xsd:boolean(?visible_str) as ?visible)
  bind(coalesce(?vivo_role,vivo:ResearcherRole) as ?role)
  bind(coalesce(?vivo_role_abbrev,"Res") as ?role_abbrev)

  bind(uri(concat(str(?grant),'#start_date')) as ?sd)
  bind(uri(concat(str(?grant),'#end_date')) as ?ed)
  bind(uri(concat(str(?grant),'#interval')) as ?interval)
  bind(uri(concat(str(?grant),"#funder")) as ?funder)

  bind(concat(?user_name_last,coalesce(concat(', ',?user_name_first),"")) as ?user_name)

  bind(concat(?role_abbrev,": ",?user_name) as ?role_name)

  
  bind(replace(lcase(concat(?user_name_last,coalesce(concat('_',?user_name_first),""))),"[^a-z_]","") as ?user_name_match)
  bind(replace(lcase(concat(?user_name_last,
                            coalesce(concat('_',
                                            if(contains(?user_name_first," "),
                                               strbefore(?user_name_first," "),
                                               ?user_name_first)),""))),"[^a-z_]","") as ?user_name_match_first)
  bind(replace(lcase(concat(?user_name_last,coalesce(concat('_',?user_name_initials),""))),"[^a-z_]","") as ?user_name_match_i)
  bind(replace(lcase(concat(?user_name_last,coalesce(concat('_',substr(?user_name_initials,1,1)),""))),"[^a-z_]","") as ?user_name_match_f)


  bind(concat(?labeled_only_name_last,coalesce(concat(', ',?labeled_only_name_first),"")) as ?labeled_only_name)

  bind(replace(lcase(concat(?labeled_only_name_last,coalesce(concat('_',?labeled_only_name_first),""))),"[^a-z_]","") as ?labeled_only_name_match)
  bind(replace(lcase(concat(?labeled_only_name_last,
                            coalesce(concat('_',
                                            if(contains(?labeled_only_name_first," "),
                                               strbefore(?labeled_only_name_first," "),
                                               ?labeled_only_name_first)),""))),"[^a-z_]","") as ?labeled_only_name_match_first)
  bind(replace(lcase(concat(?labeled_only_name_last,coalesce(concat('_',?labeled_only_name_initials),""))),"[^a-z_]","") as ?labeled_only_name_match_i)
  bind(replace(lcase(concat(?labeled_only_name_last,coalesce(concat('_',substr(?labeled_only_name_initials,1,1)),""))),"[^a-z_]","") as ?labeled_only_name_match_f)

  bind(uri(concat(str(?grant),"#",?labeled_only_name_match)) as ?labeled_only_person)
  bind(uri(concat(str(?grant),"#vcard_",?labeled_only_name_match)) as ?labeled_only_person_vcard)

  bind(concat("COPI: ",?labeled_only_name) as ?labeled_only_role_name)
  bind(if(!(?user_name_match = ?labeled_only_name_match ||
     ?user_name_match_first = ?labeled_only_name_match_first ||
     ?user_name_match_i = ?labeled_only_name_match_i ||
       ?user_name_match_f = ?labeled_only_name_match_f),
          uri(concat(str(?grant),"#roleof_",?labeled_only_name_match)),
          ?undefined)
       as ?labeled_only_role)

  bind(if(regex(?pi_name_cap,","),
          js:capitalizeName(replace(?pi_name_cap,",\\s*",", ")),
          js:capitalizeName(replace(?pi_name_cap,"(.*) ([^ ]*)$","?2, ?1"))
          ) as ?pi_name_cp)
  bind(replace(?pi_name_cp,",.*$","") as ?pi_labeled_only_name_last)
  bind(replace(?pi_name_cp,"^.*, ","") as ?pi_labeled_only_name_first)
  bind(concat(?pi_labeled_only_name_last,coalesce(concat(', ',?pi_labeled_only_name_first),"")) as ?pi_labeled_only_name)

  bind(replace(lcase(concat(?pi_labeled_only_name_last,coalesce(concat('_',?pi_labeled_only_name_first),""))),"[^a-z_]","") as ?pi_labeled_only_name_match)
  bind(replace(lcase(concat(?pi_labeled_only_name_last,
                            coalesce(concat('_',
                                            if(contains(?pi_labeled_only_name_first," "),
                                               strbefore(?pi_labeled_only_name_first," "),
                                               ?pi_labeled_only_name_first)),""))),"[^a-z_]","") as ?pi_labeled_only_name_match_first)
  bind(replace(lcase(concat(?pi_labeled_only_name_last,coalesce(concat('_',substr(?pi_labeled_only_name_first,1,1)),""))),"[^a-z_]","") as ?pi_labeled_only_name_match_f)

  bind(uri(concat(str(?grant),"#",?pi_labeled_only_name_match)) as ?pi_labeled_only_person)
  bind(uri(concat(str(?grant),"#vcard_",?pi_labeled_only_name_match)) as ?pi_labeled_only_person_vcard)

  bind(concat("PI: ",?pi_labeled_only_name) as ?pi_labeled_only_role_name)
  bind(if(!(?user_name_match = ?pi_labeled_only_name_match ||
     ?user_name_match_first = ?pi_labeled_only_name_match_first ||
       ?user_name_match_f = ?pi_labeled_only_name_match_f),
          uri(concat(str(?grant),"#roleof_",?pi_labeled_only_name_match)),
          ?undefined)
       as ?pi_labeled_only_role)


  bind(js:capitalizeTitle(?funder_label_cap) as ?funder_label)

  bind(if(xsd:date(?end_date) < xsd:date(now()), "Completed", "Active") as ?status)
  bind(concat(?grant_title," § ",
              coalesce(?status,""),
              " • ",coalesce(concat(?start_year," - ",?end_year),""),
              " • ",coalesce(?pi_labeled_only_name,""),
              " § ",
              coalesce(?funder_label,"")," • ",
              coalesce(?sponsor_award_id,"")) as ?grant_label)
}
