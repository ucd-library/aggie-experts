PREFIX : <http://oapolicy.universityofcalifornia.edu/vocab#>
PREFIX cdl: <http://oapolicy.universityofcalifornia.edu/>
PREFIX experts: <http://experts.ucdavis.edu/>
PREFIX list: <http://jena.apache.org/ARQ/list#>
PREFIX oap: <http://oapolicy.universityofcalifornia.edu/vocab#>
PREFIX person: <http://experts.ucdavis.edu/person/>
PREFIX ucdlib: <http://schema.library.ucdavis.edu/schema#>
PREFIX vivo: <http://vivoweb.org/ontology/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

construct {
  ?authorship a vivo:Authorship;
              ucdlib:favorite ?favorite;
              ucdlib:is-visible ?visible;
              vivo:relates ?expert,?publication;
              vivo:rank ?user_rank;
    .
}
WHERE {
  graph cdl: {
    ?RELATIONSHIP__ :type "publication-user-authorship";
                  :is-visible ?visible_str;
                  :type ?type;
                  :related ?pub;
                  :related ?user;
                  .

    bind(xsd:boolean(?visible_str) as ?visible)
    ?user :category "user";
          :username ?username;
          .

    ?pub :category "publication";
         .

    # Get authors from priority source
    {
    select ?pub (min(?p) as ?author_priority)
    WHERE {
      VALUES (?source ?p) {
    ("verified-manual" 1)("dimensions" 2)("pubmed" 3)("scopus" 4)("wos" 5)
    ("wos-lite" 6)("crossref" 7)("epmc" 8)("arxiv" 9)("orcid" 10)("dblp" 11)
    ("cinii-english" 12)("repec" 13)("figshare" 14)
    ("cinii-japanese" 15)("manual" 16)("dspace" 17) }

      ?pub oap:records/oap:record [ oap:source-name  ?source;
                                    oap:native/oap:field/oap:name "authors" ].
    } group by ?pub
  }
  VALUES (?source ?author_priority) {
    ("verified-manual" 1)("dimensions" 2)("pubmed" 3)("scopus" 4)("wos" 5)
    ("wos-lite" 6)("crossref" 7)("epmc" 8)("arxiv" 9)("orcid" 10)("dblp" 11)
    ("cinii-english" 12)("repec" 13)("figshare" 14)
    ("cinii-japanese" 15)("manual" 16)("dspace" 17) }

    ?pub oap:records/oap:record [ oap:source-name  ?source;
                                  oap:native/oap:field ?field ].

    OPTIONAL {
      ?field oap:name "authors";
             oap:people/oap:person [ list:index(?pos ?elem) ] .
	    ?elem oap:links/oap:link ?user.
    }
    # get any rank?
    OPTIONAL {
    ?pub oap:records/oap:record/oap:native/oap:field ?any_link.
       ?any_link oap:name "authors";
          	oap:people/oap:person [ list:index(?any_pos ?any_elem) ] .
    ?any_elem oap:links/oap:link ?user.
    }
    # Prefer the rank from the priority source, this could have multiple values
    BIND(coalesce(?pos,?any_pos)+1 as ?user_rank)

    OPTIONAL {
	    ?RELATIONSHIP__ :is-favourite "true".
    	bind(true as ?favorite)
    }
  }
  bind(uri(replace(str(?RELATIONSHIP__),str(cdl:),concat(str(experts:),"ark:/87287/d7mh2m/relationship/"))) as ?authorship)
  bind(uri(replace(str(?pub),str(cdl:),concat(str(experts:),"ark:/87287/d7mh2m/publication/"))) as ?publication)
  bind(uri(concat(str(person:),md5(?username))) as ?expert)
}
