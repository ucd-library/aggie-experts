prefix : <http://oapolicy.universityofcalifornia.edu/vocab#>
prefix cdl: <ark:/87287/d7_cdltest_/>
prefix experts: <http://experts.ucdavis.edu/>
prefix oap: <http://oapolicy.universityofcalifornia.edu/>
prefix person: <http://experts.ucdavis.edu/person/>
prefix ucdlib: <http://schema.library.ucdavis.edu/schema#>
prefix vivo: <http://vivoweb.org/ontology/core#>

select distinct (?relationship as ?RELATIONSHIP__) ?filename where {
  graph ?g {
    ?relationship :type "publication-user-authorship";
                  :effective-privacy-level "Public";
                  :is-visible "true";
                  :related ?pub;
                  :related ?user;
                  .
    ?pub :category "publication";
         .
    ?user :category "user";
          :username ?username;
          .
  }

  bind(replace(str(?relationship),str(oap:),"ark:/87287/d7mh2m/authorship/") as ?id)
  bind(concat("authorship/",?id,".jsonld.json") as ?filename)
  filter(regex(?username,"@ucdavis.edu"))
}
